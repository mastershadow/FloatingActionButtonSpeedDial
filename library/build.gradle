import net.ltgt.gradle.errorprone.CheckSeverity

/*
 * Copyright 2020 Roberto Leinardi.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
id 'com.android.library'
id 'net.ltgt.errorprone'
    }

ext {
    PUBLISH_GROUP_ID = 'com.leinardi.android'
    PUBLISH_VERSION = build_versions.version_name
    PUBLISH_ARTIFACT_ID = 'speed-dial'
}

apply from: 'publish-module.gradle'

android {
    namespace = 'com.leinardi.android.speeddial'

    defaultConfig {
        compileSdk build_versions.target_sdk
        minSdk build_versions.min_sdk
        targetSdk build_versions.target_sdk
        consumerProguardFiles 'consumer-proguard-rules.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility build_versions.java_version
        targetCompatibility build_versions.java_version
    }
}

tasks.withType(JavaCompile).configureEach {
    // remove the if condition if you want to run NullAway on test code
    if (!name.toLowerCase().contains("test")) {
        options.errorprone {
            check("NullAway", CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "com.leinardi.android.speeddial")
        }
    }
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.4")

    api "androidx.appcompat:appcompat:$versions.androidx_appcompat"
    api "com.google.android.material:material:$versions.android_material"
    api "androidx.cardview:cardview:$versions.androidx_cardview"
    errorproneJavac "com.google.errorprone:javac:$versions.errorprone_javac"
    errorprone "com.google.errorprone:error_prone_core:$versions.errorprone_core"
    annotationProcessor "com.uber.nullaway:nullaway:$versions.nullaway"
}
